global string $buildFDButton;
global int $unconnectedDomainNum;
global string $buildFDButton;
global string $domainsToBeConnected;
global int $connectingBlock[];
global int $connectedCurrentBlock;
global string $connectingOKButton;

global proc createConnectingGUI(){
	global int $unconnectedDomainNum;
	global string $buildFDButton;
	global string $domainsToBeConnected;
	global string $connectingOKButton;
	global int $connectedCurrentBlock;
	$connectedCurrentBlock=1;
	window -title "Connecting Command" -w 300 -h 240 ConnectingWindow;
	columnLayout;
		rowLayout -numberOfColumns 2 -columnWidth2 160 35;
			$buildFDButton=`button -label "Build F Domain Graph" -command "buildFDomain()"`;
		setParent..;
	setParent..;

	frameLayout -label "connecting info" -collapsable 1 -w 200;
		columnLayout;
			string $unconnectedDomain="unconnected domains: "+$unconnectedDomainNum;
			rowLayout -numberOfColumns 1 -columnWidth 310 35;
				$domainsToBeConnected=`text -label $unconnectedDomain`;
			setParent..;
		setParent..;

		columnLayout;
			rowLayout -numberOfColumns 2 -columnWidth2 95 35;
				button -label "connect" -command "connectingBlocks()";
				button -label "highlight blocks" -command "highlightBlocks()";
			setParent..;
		setParent..;
	setParent..;

	columnLayout;
		rowLayout -numberOfColumns 2 -columnWidth2 160 35;
			button -label "next" -command "gettingBlocks()";
			$connectingOKButton=`button -label "OK" -enable false`;
		setParent..;
	setParent..;

	showWindow ConnectingWindow;
}

global proc buildFDomain(){
	global string $buildFDButton;
	createNode connectingNode -n "connectingNode2015";
	button -edit -enable false $buildFDButton;
}

global proc gettingBlocks(){
	global int $connectedCurrentBlock;
	global int $unconnectedDomainNum;
	global string $connectingOKButton;
	if($connectedCurrentBlock==0){	
		confirmDialog -title "ERROR" -message "Please connect two separate block first!" -button "OK";
		return;
	}

	setAttr connectingNode2015.tri true;
	setAttr connectingNode2015.tmp1 1;
	$connectedCurrentBlock=0;

	if($unconnectedDomainNum==1){
		button -edit -enable true $connectingOKButton;
	}
}

global proc connectingBlocks(){
	global int $unconnectedDomainNum;
	global int $connectedCurrentBlock;
	string $twoDomains[]=`ls-sl`;
	int $domainNum[],$domainsInBlock,$stringSize;
	$domainsInBlock=0;
	
	if($unconnectedDomainNum==1){
		confirmDialog -title "ERROR" -message "Only one block exist, push OK to go on" -button "OK";
		return;
	}

	if($connectedCurrentBlock==1){
		confirmDialog -title "ERROR" -message "already connected, please push next first" -button "OK";
		return;
	}

	if(size($twoDomains)!=2){
		confirmDialog -title "ERROR" -message "Please select exactly two domains!" -button "OK";
		return;
	}

	$stringSize=`size $twoDomains[0]`;
	$domainNum[0]=`substring $twoDomains[0] 11 $stringSize`;
	$stringSize=`size $twoDomains[1]`;
	$domainNum[1]=`substring $twoDomains[1] 11 $stringSize`;
	if(checkSelectedDomainInBlock($domainNum[0])==1) $domainsInBlock++;
	if(checkSelectedDomainInBlock($domainNum[1])==1) $domainsInBlock++;
	if($domainsInBlock==0){
		confirmDialog -title "ERROR" -message "Cannot select two domains outside the block!" -button "OK";
		return;
	}
	if($domainsInBlock==2){
		confirmDialog -title "ERROR" -message "Cannot select two domains inside the block!" -button "OK";
		return;
	}
	$connectedCurrentBlock=1;
	setAttr connectingNode2015.index1 $domainNum[0];
	setAttr connectingNode2015.index2 $domainNum[1];
	setAttr connectingNode2015.tri true;
	setAttr connectingNode2015.tmp1 1;
}

global proc int checkSelectedDomainInBlock(int $domainIndex){
	global int $connectingBlock[];
	int $length;
	$length=size($connectingBlock);
	for($i=0;$i<$length;++$i){
		if($domainIndex==$connectingBlock[$i]) return 1;
	}
	return 0;
}

global proc highlightBlocks(){
	global int $connectingBlock[];
	int $length=size($connectingBlock);
	string $com="select ";
	for($i=0;$i<$length;++$i){
		$com+="instancing"+$connectingBlock[$i]+" ";
	}
	$com+=";";
	eval($com);
	viewFit;
}